apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'nexus'
apply plugin: 'clover'

group = 'org.gradle.api.plugins'
version = '0.6'
defaultTasks 'clean', 'build'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-clover-plugin:0.5.3'
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    groovy localGroovy()
    compile gradleApi()
    testCompile('org.spockframework:spock-core:0.5-groovy-1.8') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    clover 'com.cenqua.clover:clover:3.1.10'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Clover plugin',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty("user.name"),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty("java.version")
    }
}

idea.project {
    jdkName = '1.6'

    ipr.withXml { provider ->
        def node = provider.asNode()

        // Use GIT
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'

        // Set Gradle home
        def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
    }
}

clover {
    report {
        html = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.4'
}

modifyPom {
    project {
        name 'Gradle Clover plugin'
        description 'Gradle plugin for generating a code coverage report using Clover.'
        url 'https://github.com/bmuschko/gradle-clover-plugin'
        inceptionYear '2012'

        scm {
            url 'https://github.com/bmuschko/gradle-clover-plugin'
            connection 'scm:https://bmuschko@github.com/bmuschko/gradle-clover-plugin.git'
            developerConnection 'scm:git://github.com/bmuschko/gradle-clover-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'bmuschko'
                name 'Benjamin Muschko'
                email 'benjamin.muschko@gmail.com'
            }
        }
    }
}
