apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'clover'

version = '0.5.3'
defaultTasks 'clean', 'build'

buildscript {
    repositories {
        add(new org.apache.ivy.plugins.resolver.URLResolver()) {
            name = 'GitHub'
            addArtifactPattern 'http://cloud.github.com/downloads/[organisation]/[module]/[module]-[revision].[ext]'
        }
    }

    dependencies {
        classpath 'bmuschko:gradle-clover-plugin:0.5'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    groovy localGroovy()
    compile gradleApi()
    testCompile('org.spockframework:spock-core:0.5-groovy-1.8') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    clover 'com.cenqua.clover:clover:3.1.8'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Clover plugin',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty("user.name"),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty("java.version")
    }
}

idea.project {
    jdkName = '1.6'

    ipr.withXml { provider ->
        def node = provider.asNode()

        // Use GIT
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'

        // Set Gradle home
        def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
    }
}

clover {
    report {
        html = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.2'
}
