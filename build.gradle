buildscript {
    repositories {
        maven { url 'http://jcenter.bintray.com' }
        mavenCentral()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group = 'com.miovision'
version = '3.0.0.mio'
defaultTasks 'clean', 'build'

sourceSets {
    integrationTest {
        groovy.srcDir file('src/integTest/groovy')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integrationTest(type: Test, dependsOn: test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude group: 'org.codehaus.groovy'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Clover plugin',
                'Implementation-Version': version,
                'Built-By': System.getProperty("user.name"),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty("java.version")
    }
}

idea.project {
    jdkName = '1.6'

    ipr.withXml { provider ->
        def node = provider.asNode()

        // Use GIT
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'

        // Set Gradle home
        def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
    }
}

idea.module {
    testSourceDirs += file('src/integTest/groovy')
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier 'groovydoc'
    from groovydoc.destinationDir
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle Clover plugin')
                root.appendNode('description', 'Gradle plugin for generating a code coverage report using Clover.')
                root.appendNode('url', 'https://github.com/bmuschko/gradle-clover-plugin')
                root.appendNode('inceptionYear', '2012')

                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/bmuschko/gradle-clover-plugin')
                scm.appendNode('connection', 'scm:https://bmuschko@github.com/bmuschko/gradle-clover-plugin.git')
                scm.appendNode('developerConnection', 'scm:git://github.com/bmuschko/gradle-clover-plugin.git')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')
                def bmuschko = developers.appendNode('developer')
                bmuschko.appendNode('id', 'bmuschko')
                bmuschko.appendNode('name', 'Benjamin Muschko')
                bmuschko.appendNode('email', 'benjamin.muschko@gmail.com')
                def akowpak = developers.appendNode('developer')
                akowpak.appendNode('id', 'akowpak')
                akowpak.appendNode('name', 'Andrew Kowpak')
                akowpak.appendNode('email', 'akowpak@miovision.com')
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key =  System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    publish = true
    pkg {
        userOrg = 'miovision'
        repo = 'maven'
        name = 'gradle-clover-plugin'
        desc = 'Gradle plugin for generating a code coverage report using Clover.'
        websiteUrl = "https://github.com/miovision/${project.name}"
        issueTrackerUrl = "https://github.com/miovision/${project.name}/issues"
        vcsUrl = "https://github.com/miovision/${project.name}.git"
        licenses = ['Apache-2.0']
        labels = ['gradle', 'code', 'coverage', 'metrics', 'test', 'report', 'clover']

        version {
            name = '3.0.0.mio'
            vcsTag = "v$project.version"
            attributes = ['gradle-plugin': "com.bmuschko.clover:${project.group}:${project.name}"]
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}