plugins {
    id 'com.bmuschko.clover'
    id 'java'
}

apply from: '../deps.gradle'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation deps.junit
    clover deps.clover
}

sourceSets {
    integTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath = output + compileClasspath
    }
}

processIntegTestResources {
    duplicatesStrategy = 'exclude'
}

import org.gradle.util.GradleVersion

task integrationTest(type: Test, dependsOn: test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    if (GradleVersion.version('4.0') >= GradleVersion.current())
        testClassesDir = sourceSets.integTest.output.classesDir
    else
        testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
}

check.dependsOn integrationTest

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(GroovyCompile) {
    groovyOptions.encoding = 'UTF-8'
    options.encoding = 'UTF-8'
}

sourceCompatibility = '1.8'

// Need to wire the instrumentation task to the compile task when
// using the additional tests facility because we loose visibility
// into the task dependencies of the source sets injected
afterEvaluate {
    cloverInstrumentCodeForTest {
        dependsOn 'compileIntegTestJava'
    }
}

clover {
    additionalTestSourceSet {
        srcDirs = sourceSets.integTest.java.srcDirs
        if (GradleVersion.version('4.0') >= GradleVersion.current())
            classesDir = sourceSets.integTest.output.classesDir
        else
            classesDir = sourceSets.integTest.java.outputDir
    }

    compiler {
        encoding = 'UTF-8'
    }

    report {
        json = true
        html = true
        pdf = true
    }
}
