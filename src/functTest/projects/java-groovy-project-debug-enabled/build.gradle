plugins {
    id 'com.bmuschko.clover'
    id 'groovy'
}

apply from: '../deps.gradle'

repositories {
    mavenCentral()
}

boolean isGradle6OrLess = gradle.gradleVersion.split('\\.')[0].toInteger() < 7
def spockVersion = isGradle6OrLess ? '1.0-groovy-2.4' : '2.0-groovy-3.0'

test {
    if (isGradle6OrLess) {
        useJUnit()
    } else {
        useJUnitPlatform()
    }
}

dependencies {
    implementation localGroovy()
    testImplementation("org.spockframework:spock-core:$spockVersion") {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation deps.junit
    clover deps.clover
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(GroovyCompile) {
    groovyOptions.encoding = 'UTF-8'
    options.encoding = 'UTF-8'
}

sourceCompatibility = '1.8'

clover {
    compiler {
        encoding = 'UTF-8'
        debug = true
    }

    report {
        json = true
        html = true
        pdf = true
    }
}
