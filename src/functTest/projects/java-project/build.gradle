plugins {
    id 'com.bmuschko.clover'
    id 'java'
}

apply from: '../deps.gradle'

repositories {
    jcenter()
}

dependencies {
    testCompile deps.junit
    clover deps.clover
    compileOnly group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
    testRuntime group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(GroovyCompile) {
    groovyOptions.encoding = 'UTF-8'
    options.encoding = 'UTF-8'
}

clover {
    testIncludes = [ '**/*.java' ]
    // Prove testExcludes works and supports
    // an all-but these files scheme.
    testExcludes = [ '**/Mock*.java' ]

    compiler {
        encoding = 'UTF-8'
    }

    report {
        json = true
        html = true
        pdf = true

        // Test support for Clover's JUnit results coverage reporting
        testResultsDir = project.tasks.getByName('test').reports.junitXml.destination
        testResultsInclude = 'TEST-*.xml'

        columns {
            coveredMethods format: 'longbar', min: '75'
            coveredStatements format: '%'
            coveredBranches format: 'raw'
            totalPercentageCovered format: '%', scope: 'package'
        }

        historical {
            enabled = true
            added {
                range = 10
                interval = '3 weeks'
            }
            mover {
                threshold = 1
                range = 10
                interval = '3 weeks'
            }
            mover {
                threshold = 1
                range = 10
                interval = '3 months'
            }
            mover {
                threshold = 1
                range = 10
                interval = '1 year'
            }
        }
    }
}
