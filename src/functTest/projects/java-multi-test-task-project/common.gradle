
apply from: '../deps.gradle'
apply plugin: 'java'
apply plugin: 'com.bmuschko.clover'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation deps.junit
    testRuntimeOnly deps.junit
    clover deps.clover
}

configurations {
    customTestImplementation.extendsFrom testImplementation
    customTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
    customTest {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

import org.gradle.util.GradleVersion

task customTest(type: Test) {
    if (GradleVersion.version('4.0') >= GradleVersion.current())
        testClassesDir = sourceSets.customTest.output.classesDir
    else
        testClassesDirs = sourceSets.customTest.output.classesDirs
    classpath = sourceSets.customTest.runtimeClasspath
}

check.dependsOn customTest

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(GroovyCompile) {
    groovyOptions.encoding = 'UTF-8'
    options.encoding = 'UTF-8'
}

sourceCompatibility = '1.8'

clover {
    compiler {
        encoding = 'UTF-8'
    }

    report {
        json = true
        html = true
        pdf = true
    }
}
